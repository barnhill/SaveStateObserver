plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.dokka)
    alias(libs.plugins.toml.version.checker)
}

version = "2.10.0"
group = "com.pnuema.android"
archivesBaseName = 'savestateobserver'

ext {
    baseUrl = 'https://github.com/barnhill'
    siteUrl = 'https://github.com/barnhill/savestateobserver'
    gitUrl = 'https://github.com/barnhill/savestateobserver.git'
    descr = 'Android tool to output the contents sizes of saved state bundles in onSaveInstance state calls'
}

android {
    namespace = 'com.pnuema.android.savestateobserver'
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName version
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    kotlin {
        jvmToolchain(17)
    }
}

dependencies {
    implementation (libs.androidx.appcompat)
    implementation (libs.androidx.startup)
    testImplementation (libs.junit)
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

build.dependsOn dokkaJavadoc